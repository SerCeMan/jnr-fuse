plugins {
    id 'java'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'net.researchgate.release' version '2.4.0'
    id "com.jfrog.bintray" version "1.1"
    id 'maven'
}

repositories {
    jcenter()
}

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
    }
}

shadowJar {
    relocate 'org.objectweb.asm', 'org.objectweb.asm.shaded'
}


if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}


dependencies {
    compile 'com.github.jnr:jnr-ffi:2.1.4'
    compile 'com.github.jnr:jnr-posix:3.0.37'
    compile 'com.github.jnr:jnr-constants:0.9.8'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

release {
    failOnCommitNeeded = false
    newVersionCommitMessage = '[jnr-fuse release] - '
    preTagCommitMessage = '[jnr-fuse release] - pre tag commit: '
    buildTasks = ['clean', 'assemble', 'bintrayUpload']
}

bintray {
    user = 'serce'
    key = "${System.getenv('BINTRAY_KEY')}"
    configurations = ['archives']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = 'jnr-fuse'
        version {
            name = project.version
        }
    }
}
